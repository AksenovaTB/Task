#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	// {{  Аксенова Т.Б. Доработка КЛ
	// дополение форм элементом КонтактноеЛицо
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПоляСкидкаВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма); 		
	КонецЕсли;
	// }}  Аксенова Т.Б. Доработка КЛ
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	// {{  Аксенова Т.Б. Доработка КЛ
	// создание элемента КонтактноеЛицо в документах с табличной частью
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	// }}  Аксенова Т.Б. Доработка КЛ
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	// {{  Аксенова Т.Б. Доработка КЛ 
	// создание элемента КонтактноеЛицо в документах без табличной части
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	// }}  Аксенова Т.Б. Доработка КЛ
КонецПроцедуры  

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	// {{  Аксенова Т.Б. Доработка КЛ 
	// создание элемента КонтактноеЛицо в документах без табличной части
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	// }}  Аксенова Т.Б. Доработка КЛ
КонецПроцедуры  


Процедура ДобавитьПоляСкидкаВГруппаШапкаЛево(Форма)
	// {{  Аксенова Т.Б. Доработка КЛ
	// создание поля СогласованнаяСкидка и команды ПересчитатьТаблицу
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Вставить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КЛ_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчитатьТаблицы = Форма.Команды.Добавить("ПересчитатьТаблицы");
	КомандаПересчитатьТаблицы.Заголовок = "Пересчитать таблицы";
	КомандаПересчитатьТаблицы.Действие = "КомандаПересчитатьТаблицы";
	
	КнопкаКоманды = Форма.Элементы.Вставить("КнопкаПересчитатьТаблицы", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаКоманды.ИмяКоманды = "ПересчитатьТаблицы";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаКоманды.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Картинка = БиблиотекаКартинок.Обновить;
	КнопкаКоманды.Картинка = Картинка;
	// }}  Аксенова Т.Б. Доработка КЛ
КонецПроцедуры


#КонецОбласти